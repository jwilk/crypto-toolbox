#!/usr/bin/env perl

# Copyright © 2020-2022 Jakub Wilk <jwilk@jwilk.net>
# SPDX-License-Identifier: MIT

# http://csrc.nist.gov/groups/STM/cmvp/documents/fips140-2/FIPS1402IG.pdf
# § 7.5 Strength of Key Establishment Methods

use v5.14;

no lib '.';  # CVE-2016-1238

use strict;
use warnings (
    NONFATAL => qw(all),
    FATAL => qw(numeric)
);

use English qw(-no_match_vars);

my $prog = $PROGRAM_NAME =~ s{.*/}{}r;
my $help = "Usage: $prog NBITS";
if (scalar @ARGV != 1) {
    say {*STDERR} $help;
    exit 1;
}
if ($ARGV[0] eq '-h' || $ARGV[0] eq '--help') {
    say $help;
    exit 0;
}
my $L = $ARGV[0];
my $x = (1.923 * ($L * log(2) * log($L * log(2)) ** 2) ** (1/3) - 4.69) / log(2);
if ($x < 0) {
    $x = 0;
}
printf "%.0f\n", $x;

# vim:ts=4 sts=4 sw=4 et
